grammar org.big.erd.EntityRelationship with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate entityRelationship "http://www.big.org/erd/EntityRelationship"

Model:
	'erdiagram' name=ID
	(notation=NotationOption?)
	(entities+=Entity | relationships+=Relationship)*;

NotationOption:
	'notation' '=' notationType=NotationType;

Entity:
	(weak?='weak')? 'entity' name=ID ('extends' extends=[Entity])? '{'
		(attributes += Attribute)*
	'}';

Relationship:
	(weak?='weak')? 'relationship' name=ID '{'
		(first=RelationEntity ((firstType=RelationshipType second=RelationEntity) (secondType=RelationshipType third=RelationEntity)?)?)?
		(attributes += Attribute)*
	'}';

RelationEntity:
	target=[Entity] ('[' 
		cardinality=CardinalityType ('|' role=STRING)?
	']')?;

Attribute:
	(visibility=VisibilityType)? name=ID (':' datatype=DataType)? (type=AttributeType)?;

DataType:
	type=ID ('(' size=INT ')')?;  

@Override	
terminal INT returns ecore::EInt:
    ('0'..'9')+;

enum AttributeType:
	NONE = 'none' | 
	KEY = 'key' | 
	PARTIAL_KEY = 'partial-key' | 
	OPTIONAL = 'optional' |  
	DERIVED = 'derived' | 
	MULTIVALUED = 'multivalued';

enum CardinalityType:
	NONE = 'NONE' | 
	ZERO_OR_ONE = '0..1' | 
	ZERO_OR_MORE = '0..N' | 
	// ignore warning - same enum assignment is intentional to support both notations
	ONE = '1' | ONE = '1..1' |
	MANY = 'N' | MANY = '1..N';

enum NotationType:
    DEFAULT='default' | 
    CHEN='chen' | 
    BACHMAN='bachman' | 
    CROWSFOOT='crowsfoot'|
    UML='uml'; 
 
enum RelationshipType:
	DEFAULT='->'|
	AGGREGATION_LEFT='o-'|
	AGGREGATION_RIGHT='-o'|
	COMPOSITION_LEFT='*-'|
	COMPOSITION_RIGHT='-*';
	
enum VisibilityType:
	NONE='none' |
	PUBLIC='+'|
	PRIVATE='-'|
	PROTECTED='#'|
	PACKAGE='~'; 
 